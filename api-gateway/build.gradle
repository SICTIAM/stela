plugins {
    id "com.moowork.node" version "1.1.1"
}

archivesBaseName = 'stela-api-gateway'

node {
    version = '6.5.0'
    npmVersion = '3.10.6'
    download = true
}

repositories {
    // TEMP : while the new version of the ozwillo-java-spring-integration lib is not published on the Maven repos
    // meanwhile, run ./gradlew install in the lib's project to have it locally published
    mavenLocal()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    compile('org.springframework.cloud:spring-cloud-starter-netflix-zuul')
    compile('org.springframework.cloud:spring-cloud-starter-security')
    compile("org.springframework.retry:spring-retry")
    compile("org.springframework.boot:spring-boot-starter-aop")
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile('com.ozwillo:ozwillo-java-spring-integration:1.25.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

configure(tasks.processResources) {
    // we don't want source JSX in the jar, we only need their transpiled, bundled version in build/
    exclude 'jsx/'
    exclude 'build/'
    exclude 'styles/'
}

task frontBundle(type: NpmTask) {
    args = ['run', 'build']
    doLast {
        copy {
            from "${sourceSets.main.resources.srcDirs[0]}/build/static"
            into "${sourceSets.main.output.resourcesDir}/public/static"
        }
        copy {
            from "${sourceSets.main.resources.srcDirs[0]}/build/index.html"
            into "${sourceSets.main.output.resourcesDir}/public/"
        }
        copy {
            from "${sourceSets.main.resources.srcDirs[0]}/build/asset-manifest.json"
            into "${sourceSets.main.output.resourcesDir}/public/"
        }
    }
}

bootJar.dependsOn(frontBundle)
frontBundle.dependsOn(npmInstall)
