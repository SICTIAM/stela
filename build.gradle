buildscript {
	ext {
		springBootVersion = '2.0.0.M3'
	}
	repositories {
		mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('se.transmode.gradle:gradle-docker:1.2')
	}
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'


	apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
	apply plugin: 'docker'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
		maven {
			// docker-compose-rule is published on bintray
			url 'https://dl.bintray.com/palantir/releases'
		}
	}

	dependencies {
        compile('org.springframework.boot:spring-boot-devtools')
        compile('ch.qos.logback:logback-classic:1.1.5')
        runtime('org.flywaydb:flyway-core')
        testCompile('junit:junit:4.12')
        testCompile('com.palantir.docker.compose:docker-compose-rule-junit4:0.31.1')

    }

    ext {
        springCloudVersion = 'Finchley.M1'
    }

    bootRun {
        environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "dev"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    bootJar {
        launchScript {
            included = true
        }
    }

    task image(type: Docker, dependsOn: build) {
        push = false
        applicationName = jar.baseName
        dockerfile = file('src/main/docker/Dockerfile')
        doFirst {
            copy {
                from jar
                into stageDir
            }
        }
    }
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.4.1'
}
